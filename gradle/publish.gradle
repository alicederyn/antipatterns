apply plugin: 'maven-publish'

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    classifier 'sources'
    baseName = "${project.name}-unshaded"
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
    destinationDir rootProject.file("${buildDir}/libs")
}

shadowJar {
    relocate 'com.google', 'com.palantir.antipatterns.shaded.guava'
    baseName = project.name
    classifier = null
}

tasks.build.dependsOn sourceJar, javadocJar

publishing {
    publications {
        Antipatterns(MavenPublication) {
            from components.shadow
            artifact(sourceJar)
            artifact(javadocJar)
        }
    }
}

bintray {
    user = System.env.BINTRAY_USER
    key = System.env.BINTRAY_KEY
    publications = ['Antipatterns']
    publish = true
    pkg {
        repo = 'releases'
        name = project.name
        userOrg = 'palantir'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/palantir/antipatterns.git'
    }
}

bintrayUpload.onlyIf {
    System.env.BINTRAY_USER && System.env.BINTRAY_KEY && (project.version != 'unspecified')
}

