/*
 * Copyright 2016 Palantir Technologies, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
  id 'com.github.johnrengelman.shadow' version '1.2.3'
  id 'com.jfrog.artifactory' version '4.4.4'
  id 'com.jfrog.bintray' version '1.6'
}

repositories {
  mavenCentral()
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'findbugs'

sourceCompatibility = '1.7'
targetCompatibility = '1.7'

apply from: "${rootDir}/gradle/publish.gradle"

group 'com.palantir.antipatterns'
version = System.env.CIRCLE_TAG?.replaceAll('^v','')

dependencies {
  compileOnly 'com.google.code.findbugs:findbugs:3.0.1'
  compile 'com.google.guava:guava:18.0'
  testCompile 'junit:junit:4.12'
  testCompile 'com.google.truth:truth:0.28'
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.1'
}

//// Integration test with FindBugs ////////////////////////////////////////////
sourceSets {
    integrationTest {
        java {
            srcDir 'src/integration/java'
        }
        resources {
            srcDir 'src/integration/resources'
        }
    }
}

task integrationTestReport(type: FindBugs) {
  pluginClasspath = jar.outputs.files
  classes = fileTree(project.sourceSets.integrationTest.output.classesDir)
  source = project.sourceSets.integrationTest.java.srcDirs
  classpath = files()
  ignoreFailures = true
  reportLevel = "low"
  reports {
    xml.enabled = false
    html {
      enabled = true
      destination 'build/findbugs.html'
    }
  }
}

task integrationTest(type: Exec) {
  inputs.file 'build/findbugs.html'
  commandLine 'scripts/check-integration-tests', 'build/findbugs.html'
}

integrationTestReport.dependsOn shadowJar
integrationTestReport.dependsOn integrationTestClasses
integrationTest.dependsOn integrationTestReport
integrationTest.shouldRunAfter test
check.dependsOn integrationTest
